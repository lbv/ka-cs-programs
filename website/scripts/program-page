#! /usr/bin/env coffee

fs   = require 'fs'
path = require 'path'
yaml = require 'js-yaml'

{puts} = require 'util'


usage = ->
    puts "#{process.argv[1]} YML"
    process.exit 1

usage() if process.argv.length < 3

ymlFile  = process.argv[2]
id       = path.basename ymlFile, '.yml'

yml = fs.readFileSync ymlFile, encoding: 'utf8'

prg = yaml.load yml

links = ''
links += "* [#{lnk.desc}](#{lnk.url})\n" for lnk in prg.links

todo = ''
todo += "* #{td}\n" for td in prg.todo

outFile = "src/documents/programs/#{id}.coffee"
data = """
  ##
  ## Generated by the `program-page` script
  ##

  (require 'phi-teacup').dsl()

  meta =
    title: '''#{prg.title}'''
    nav: [
      { name: 'Home', url: '' }
      { name: 'Programs', url: '#programs' }
      { name: '''#{prg.title}''' }
    ]

  template = ->
    section '.grid-100.program', ->
      img '.program-thumb', src: 'img/programs/#{id}.png'
      h1 '''#{prg.title}'''
      markdown '''#{prg.description}'''
      markdown '''**Tags**: #{prg.tags}'''

      h2 'Summary'
      markdown '''#{prg.summary}'''

      h2 'Links'
      markdown '''#{links}'''

      h2 'ChangeLog'
      markdown '''
  **Current revision**: #{prg.version}

  ```
  #{prg.changelog}
  ```
  '''

      h2 'To Do'
      markdown '''#{todo}'''

  module.exports = useLayout 'default', template, doc: meta
"""

puts "Writing #{outFile}..."
fs.writeFileSync outFile, data
